// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        gradlePluginPortal()
        google()
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath libs.gradlePlugin
        classpath libs.kotlinGradlePlugin
    }
}

plugins {
    // this is necessary to avoid the plugins to be loaded multiple times
    // in each subproject's classloader
    alias(libs.plugins.androidApplication) apply false
    alias(libs.plugins.androidLibrary) apply false
    alias(libs.plugins.composeMultiplatform) apply false
    alias(libs.plugins.composeCompiler) apply false
    alias(libs.plugins.kotlinMultiplatform) apply false

}

ext {
    localProp = new Properties()
    fileName = 'local.properties'
    if (project.rootProject.file(fileName).exists()) {
        localProp.load(new FileInputStream(rootProject.file(fileName)))
    }
    packageCloudReadToken = System.env.PACKAGECLOUD_READ_TOKEN != null ? System.env.PACKAGECLOUD_READ_TOKEN : localProp["packageCloudReadToken"] ?: ""
    packageCloudReadTokenInternal = System.env.PACKAGECLOUD_READ_TOKEN_INTERNAL ?: localProp["packageCloudReadTokenInternal"]

    githubUserName = System.env.GITHUB_PUBLISH_USERNAME ?: localProp["githubUserName"] ?: ""
    githubToken = System.env.GITHUB_PUBLISH_TOKEN ?: localProp["githubToken"] ?: ""

}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://packagecloud.io/priv/${packageCloudReadToken}/stone/pos-android/maven2" }
        maven { url "https://packagecloud.io/priv/${packageCloudReadTokenInternal}/stone/pos-android-internal/maven2" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()

        maven {
            name = "GitHubPackages SDK"
            credentials {
                username(githubUserName)
                password(githubToken)
            }
            url "https://maven.pkg.github.com/stone-payments/pos-android-sdk"
        }

    }
}

